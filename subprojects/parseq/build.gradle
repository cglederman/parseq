ext {
  description = '''ParSeq project core code'''
}


//TODO avoid buildscript dependency
buildscript {
  repositories {
    maven {
      url "https://repo1.maven.org/maven2/"
    }
    maven {
       url "https://linkedin.jfrog.io/artifactory/open-source/"
    }
  }

  dependencies {
    classpath group: 'net.sourceforge.fmpp', name: 'fmpp', version: '0.9.16'
    classpath 'com.fasterxml.jackson.core:jackson-annotations:2.14.2'
    classpath 'com.fasterxml.jackson.core:jackson-core:2.14.2'
    classpath 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
  }

  ant.taskdef(name: 'fmpp', classname:'fmpp.tools.AntTask', classpath: buildscript.configurations.classpath.asPath)
}

configurations {
  bootstrap {
    description "bootstrapping dependencies"
    visible = false
  }

  // Maintains transitive consumer compatibility for consumers of parseq 3. TODO: remove in parseq 5
  testArtifact {
    description "this is deprecated and will be removed, please depend on parseq-test-api instead"
  }
}

test { 
  useJUnitPlatform()
}
repositories {
    mavenCentral()
    mavenLocal() 
    jcenter()
}

dependencies {
  compile group: "com.google.guava", name:"guava", version: "30.1.1-jre"
  compile group: 'org.codehaus.jackson', name: 'jackson-core-asl', version: '1.8.8'
  compile group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: '1.8.8'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.2'
  compile group: "org.apache.logging.log4j", name: "log4j-core", version: "2.19.0"
  compile 'com.fasterxml.jackson.core:jackson-annotations:2.14.2'
  compile 'com.fasterxml.jackson.core:jackson-core:2.14.2'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
  compile group: "org.slf4j", name: "slf4j-api", version: "1.7.36"
  compile group: "org.apache.logging.log4j", name: "log4j-to-slf4j", version: "2.19.0"
 
  bootstrap group: "net.sourceforge.fmpp", name: "fmpp", version: "0.9.16"

  testCompile project(':parseq-test-api')
  testCompile group: "org.testng", name: "testng", version: "7.5.1"
  testCompile group: "org.slf4j", name: "slf4j-log4j12", version: "1.7.36"
  testCompile group: 'junit', name: 'junit', version: '4.13.2'
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
  testRuntimeOnly group: "org.junit.vintage", name: "junit-vintage-engine", version: "5.8.2"  
  testCompile group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.10.0'

  // TODO: remove in parseq 5
  testArtifact project(':parseq-test-api')
}

// When update the template and data in codegen, need to run this task before gradle build task
task generateFmppSources {
  description 'Configure Freemarker to generate TupleNtask, ParNTask code in source folder.'
  doLast {
    println "Generating sources...."
    ant.fmpp configuration: "$projectDir/src/main/codegen/config.fmpp",
        sourceRoot: "$projectDir/src/main/codegen/templates",
        outputRoot: "$projectDir/src/main/java"
  }
}

